buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.jimdo.gradle:gradle-apt-plugin:0.5-SNAPSHOT'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'apt'
apply plugin: 'propdeps'
apply plugin: 'propdeps-eclipse'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceSets {

    main {
        java {
            srcDirs ('src', 'Common')
            exclude '**/test/**'
        }
        resources {
            srcDir 'res'
            exclude('*/Thumbs.db')
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'testRes'
        }
    }
}

task unitTest( type: Test ) {
    exclude 'test/integration/*'
}

compileJava {
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

dependencies {
    apt 'com.google.auto.value:auto-value:1.0'
    apt 'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'

    optional 'com.google.auto.value:auto-value:1.0'
    optional 'com.google.dagger:dagger:2.0-SNAPSHOT'

    compile 'commons-io:commons-io:2.4'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'org.threeten:threetenbp:1.2'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'junit:junit:4.11'
}

jar {

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes 'Main-Class': 'aohara.tinkertime.TinkerTime'
    }
}

task release(type:Zip,dependsOn:jar) {
	jar.outputs.files.each {
		from it.getPath()
	}
	from "readme.md"
}

eclipse {
  jdt.file.withProperties {
    it['org.eclipse.jdt.core.compiler.processAnnotations'] = 'enabled'
  }
}
